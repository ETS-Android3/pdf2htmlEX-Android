/*
 * pdf2htmlEX/build.gradle
 *
 * pdf2htmlEX-Android (https://github.com/ViliusSutkus89/pdf2htmlEX-Android)
 * Android port of pdf2htmlEX - Convert PDF to HTML without losing text or format.
 *
 * Copyright (c) 2019 - 2021 Vilius Sutkus <ViliusSutkus89@gmail.com>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 3 as
 * published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

group = 'com.viliussutkus89'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31

        versionCode 25
        versionName project.version

        archivesBaseName = rootProject.name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk.abiFilters = project.hasProperty('abi') ? [ project.property('abi') ] : ["arm64-v8a", "armeabi-v7a", "x86", "x86_64"]
        externalNativeBuild.cmake.arguments "-DANDROID_STL=c++_shared"
    }
    //#UNCOMMENT-DURING-STAGING# /*
    buildTypes.release.externalNativeBuild {
      cmake.arguments '-DCMAKE_BUILD_TYPE=MinSizeRel'
    }
    externalNativeBuild.cmake {
        path "src/main/cpp/CMakeLists.txt"
        version "3.21.0"
    }
    ndkVersion "22.1.7171670"
    sourceSets {
        release {
            jniLibs.srcDirs += "${projectDir}/../dependency-builder/build/includeTheseInDownstreamAAR/release/jniLibs"
            assets.srcDirs += "${projectDir}/../dependency-builder/build/includeTheseInDownstreamAAR/release/assets/" + android.defaultConfig.ndk.abiFilters[0]
        }
        debug {
            jniLibs.srcDirs += "${projectDir}/../dependency-builder/build/includeTheseInDownstreamAAR/debug/jniLibs"
            assets.srcDirs += "${projectDir}/../dependency-builder/build/includeTheseInDownstreamAAR/debug/assets/" + android.defaultConfig.ndk.abiFilters[0]
        }
    }
    //#UNCOMMENT-DURING-STAGING# */
    buildFeatures.prefab true
}

configurations {
    javadocDependenciesToObtainClasspath
}

dependencies {
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'com.getkeepsafe.relinker:relinker:1.4.4'
    implementation 'com.viliussutkus89:tmpfile-android:3.0.4'
    implementation 'com.viliussutkus89:assetextractor-android:1.3.2'
    implementation 'com.viliussutkus89:fontconfig-android-translator:1.0.2'
    implementation 'com.viliussutkus89:ExecutableRunner:1.0.3'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    javadocDependenciesToObtainClasspath 'androidx.annotation:annotation:1.2.0'
    javadocDependenciesToObtainClasspath 'com.getkeepsafe.relinker:relinker:1.4.4'
    javadocDependenciesToObtainClasspath 'com.viliussutkus89:tmpfile-android:3.0.4'
    javadocDependenciesToObtainClasspath 'com.viliussutkus89:assetextractor-android:1.3.2'
    javadocDependenciesToObtainClasspath 'com.viliussutkus89:fontconfig-android-translator:1.0.2'
    javadocDependenciesToObtainClasspath 'com.viliussutkus89:ExecutableRunner:1.0.3'

    //#UNCOMMENT-DURING-STAGING# /*
    compileOnly project(":dependency-builder")
    //#UNCOMMENT-DURING-STAGING# */ implementation 'com.viliussutkus89:pdf2htmlex-android:#STAGING-VERSION#'
    // The line above is used by the ci-scripts/prepareLibraryForStagingRepository
}

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
    into ("", { from android.sourceSets.main.java.srcDirs} )
    into ("", { from file("../LICENSE")})
    into ("cpp", { from fileTree(dir: "src/main/cpp/") } )
    into ("dependency-builder", { from fileTree(dir: "../dependency-builder/src/main/cpp/") } )
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDependenciesToObtainClasspath
    title = "${rootProject.name} ${project.version} API"
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId rootProject.name
                version = rootProject.version

                artifact sourceJar
                artifact javadocJar

                pom {
                    name = rootProject.name
                    description = 'Android port of pdf2htmlEX - Convert PDF to HTML without losing text or format'
                    url = 'https://github.com/ViliusSutkus89/pdf2htmlEX-Android'
                    packaging = 'aar'
                    inceptionYear = '2019'
                    developers {
                        developer {
                            id = "ViliusSutkus89"
                            name = "Vilius Sutkus"
                            email = "ViliusSutkus89@gmail.com"
                        }
                    }
                    scm {
                        url = 'https://github.com/ViliusSutkus89/pdf2htmlEX-Android'
                        connection = 'https://github.com/ViliusSutkus89/pdf2htmlEX-Android.git'
                    }
                }
            }
        }
    }

    // This may look ugly...
    // but useInMemoryPgpKeys expects valid keys even with required { false }
    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}

//#UNCOMMENT-DURING-STAGING# /*
tasks.whenTaskAdded { task ->
    if (task.name == 'generatePomFileForReleasePublication') {
        def gatherLicenses = tasks.getByPath(":dependency-builder:gatherLicenses")
        task.dependsOn(gatherLicenses)
        task.doFirst {
            pom.withXml {
                def licensesNode = asNode().appendNode('licenses')
                def thisProjLicense = licensesNode.appendNode('license')
                thisProjLicense.appendNode('name', 'pdf2htmlEX-Android')
                thisProjLicense.appendNode('url', 'https://www.gnu.org/licenses/gpl-3.0.en.html')
                thisProjLicense.appendNode('distribution', 'repo')

                new XmlSlurper().parse(gatherLicenses.getOutputXmlFile().get().asFile).license.each {
                    def license = licensesNode.appendNode('license')
                    license.appendNode('name', it.dependencyName)
                    license.appendNode('url', it.licenseText)
                    license.appendNode('distribution', 'repo')
                }
            }
        }
    }
}
//#UNCOMMENT-DURING-STAGING# */
