plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.viliussutkus89.licenseOnly'

android {
    compileSdk 32
    defaultConfig {
        minSdk 16
        targetSdk 32

        versionCode 1
        versionName '1.00'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId project.name
                version = android.defaultConfig.versionName

                pom {
                    name = project.name
                    description = 'Symbol and Zapf Dingbats Type 1 fonts'
                    url = 'http://www.xpdfreader.com/download.html'
                    packaging = 'aar'
                    inceptionYear = '1995'
                    licenses {
                        license {
                            name = 'GPLv2'
                            url = 'https://www.gnu.org/licenses/gpl-2.0.html'
                            distribution = 'repo'
                        }
                    }
                    scm {
                        url = 'https://github.com/ViliusSutkus89/licenseOnly'
                        connection = 'https://github.com/ViliusSutkus89/licenseOnly.git'
                    }
                    developers {
                        developer {
                            id = "urwpp"
                            name = "ghostscript project by URW++ Design and Development Incorporated"
                        }
                    }
                }
            }
        }
    }

    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}
